apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
  compileSdkVersion androidVersions.compileSdkVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    applicationId "com.mapbox.mapboxsdk.plugins.testapp"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
    multiDexEnabled = true
  }

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      testCoverageEnabled true
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    abortOnError false
  }

  dexOptions {
    maxProcessCount 8
    javaMaxHeapSize "2g"
    preDexLibraries true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  // Kotlin
  implementation dependenciesList.kotlin

  // Mapbox
  implementation(dependenciesList.mapboxMapSdk, {
    exclude group: 'android.arch.lifecycle'
    exclude group: 'com.google.android.gms'
  })
  implementation (dependenciesList.mapboxGeocoding, {
    exclude group: 'com.squareup.okhttp3'
  })
  implementation dependenciesList.mapboxTurf

  // Architecture
  implementation dependenciesList.lifecycleExtensions
  implementation dependenciesList.roomRuntime
  implementation dependenciesList.androidxMultidex
  kapt dependenciesList.roomCompiler

  // Google Play Services
  implementation dependenciesList.playLocation

  // Support libraries
  implementation dependenciesList.supportAnnotation
  implementation dependenciesList.supportAppcompatV7
  implementation dependenciesList.supportDesign
  implementation dependenciesList.supportRecyclerView
  implementation dependenciesList.supportConstraintLayout

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito

  // Instrumentation testing
  androidTestImplementation dependenciesList.mockitoAndroid
  androidTestImplementation dependenciesList.supportAppcompatV7
  androidTestImplementation dependenciesList.testRunner
  androidTestImplementation dependenciesList.testRules
  androidTestImplementation dependenciesList.testEspressoCore
  androidTestImplementation dependenciesList.testRoom
  androidTestImplementation dependenciesList.testArchCore
  androidTestImplementation(dependenciesList.mockitoCore, {
    exclude group: 'net.bytebuddy'
  })

  // Square crew
  implementation dependenciesList.timber

  // Leak canary
  debugImplementation dependenciesList.leakCanaryDebug

  // Plugin modules
  implementation project(':plugin-traffic')
  implementation project(':plugin-building')
  implementation project(':plugin-places')
  implementation project(':plugin-offline')
  implementation project(':plugin-localization')
  implementation project(':plugin-annotation')
  implementation project(':plugin-markerview')
  implementation project(':ktx-mapbox-maps')
  implementation project(':plugin-scalebar')
}

apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/generate-token.gradle"
apply plugin: 'kotlin-android-extensions'
